#!/usr/bin/perl

# $Id$

# This program lists programs that should be intercepted to stdout.

use strict;
use warnings;
use FindBin;
use File::Spec;

my $INSTALL_PREFIX = $ENV{INSTALL_PREFIX} || $FindBin::RealBin;
my $interceptors_dir = "$INSTALL_PREFIX/lib/build-interceptor";

$::quiet = 0;

sub isprog {
    return 1 if !-d $_[0] && -x $_[0];
}

sub which {
    my ($x) = @_;
    if ($x =~ m,^/,) {
        return $x if isprog $x;
        return;
    }

    for my $dir (File::Spec->path()) {
        my $f = File::Spec->catfile($dir, $x);
        return $f if isprog($f);
    }
}

sub whichm {
    my @r = ();
    for my $prog (@_) {
        my $progpath = which($prog);
        push(@r, $progpath) if $progpath;
    }
    return @r;
}

sub sortuniq {
    my %h = ();
    for my $x (@_) {
        $h{$x} = 1;
    }
    return sort keys %h;
}

sub filter_out_orig {
    map {
        if (/_orig$/) {
            print STDERR "$0: ignoring $_\n" unless $::quiet;
            ();
        } else {
            $_;
        }
    } @_;
}

sub readlinkfull {
    my ($x) = @_;
    die unless $x;
    my $link = readlink($x);
    return unless $link;
    my ($vol,$dir,$file) = File::Spec->splitpath($x);
    my $basedir = File::Spec->catpath($vol, $dir, "");
    return File::Spec->rel2abs($link, $basedir);
}

sub find_link {
    my ($path, $list) = @_;
    die unless $path;
    while ($path = readlinkfull($path)) {
        return $path if (grep { $path eq $_ } @$list);
    }
    return;
}

sub filter_out_redundant_symlinks {
    my (@progs) = @_;
    return map {
        if (my $l = find_link($_, \@progs)) {
            print STDERR "$0: ignoring $_: it's a symlink to $l, also intercepted.\n";
            ();
        } else {
            $_;
        }
    } @progs;
}

sub is_ccache {
    my ($path) = @_;
    while ($path = readlinkfull($path)) {
        my ($vol,$dir,$file) = File::Spec->splitpath($path);
        return $file if ($file =~ /ccache/);
    }
    return 0;
}

sub filter_out_ccache {
    return map {
        if (is_ccache($_)) {

            # for now, just disallow.  Figure out later how to make it work correctly.
            die "$0: $_ appears to be a symlink to ccache; do not use Build-Interceptor in conjunction with ccache.\n";

            print STDERR "$0: ignoring $_: it's apparently a ccache symlink\n";
            ();
        } else {
            $_;
        }
    } @_;
}

sub prog_to_interceptor {
    my ($prog) = @_;
    my ($vol,$dir,$file) = File::Spec->splitpath($prog);
    my $base = $file;
    $base =~ s/-.*//;

    my $interceptor_script = "$interceptors_dir/${base}_interceptor";
    if (!-f $interceptor_script || !-x $interceptor_script) {
        die "$0: missing $interceptor_script, needed to intercept $prog\n";
    }
    return $interceptor_script;
}

# versions of gcc
my @gccs = (whichm('gcc'),
            </usr/bin/gcc-*>,
            </usr/bin/*-linux-gcc>);
{
    local $::quiet = 1;
    @gccs = filter_out_orig(@gccs);
}

# User tools to intercept.
my @usrtools = sortuniq(@gccs,
                        whichm('g++'), </usr/bin/g++-*>,
                        whichm('gcc'), </usr/bin/gcc-*>,
                        # whichm('make')
                        whichm('cc'),
                        whichm('c++'),
                        whichm('as'),
                        whichm('ld'));

@usrtools = filter_out_orig(@usrtools);

@usrtools = filter_out_redundant_symlinks(@usrtools);

@usrtools = filter_out_ccache(@usrtools);

## Internal gcc tools usually not called by the user.
# at least under gcc 3.4 this just runs ld: collect2
my @gcc_component_names = qw/cpp0 tradcpp0 cc1 cc1plus f771/;

# get full paths of gcc components
my @gcc_components = ();
for my $component_name (@gcc_component_names) {
    for my $gcc (@gccs) {
        my $t = `$gcc -print-prog-name=$component_name 2>/dev/null`; chomp $t;
        push(@gcc_components, $t) if (-f $t);
    }
}

@gcc_components = sortuniq(@gcc_components);

for my $prog (@usrtools, @gcc_components) {
    my $interceptor = prog_to_interceptor($prog);
    print "$prog=$interceptor\n";
}
