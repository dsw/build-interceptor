#!/usr/bin/perl

# $Id$

# This script implements "RENAME MODE" (see doc/modes.txt).
#
# It will move your system gcc toolchain away and point softlinks at the
# intereceptor scripts.  Generally, run targets in this makefile as root.  Use
# Makefile to build intercept.progs as a normal user.

my $SYNTAX = <<END
$0 -- manage rename-mode Build Interception.

   $0 [status]    -- show current status
   $0 on          -- turn on, i.e. modify /usr/bin/gcc, etc.
   $0 off         -- turn off, restore the way it was before 'on'

END
    ;

use strict;
use warnings;
use FindBin;
use IO::File;

my $intercept_progs_filename = "$FindBin::RealBin/intercept.progs";

my $interceptors_dir = "$FindBin::RealBin/lib/build-interceptor";

if (!-d $interceptors_dir) {
    # TODO: allow configuration
    die "$0: couldn't find interceptors directory ($interceptors_dir)\n";
}

if (!-f $intercept_progs_filename) {
    # TODO: allow intercept.progs to live in a different directory
    die "$0: couldn't find $intercept_progs_filename; run `make' first.\n";
}

my $cmd;

if (scalar(@ARGV) > 2) { syntax(); }

if (scalar(@ARGV) == 0) {
    $cmd = 'status';
} elsif ($ARGV[0] eq 'status' || $ARGV[0] eq 'print') {
    $cmd = 'status';
} elsif ($ARGV[0] eq 'on') {
    $cmd = 'on';
} elsif ($ARGV[0] eq 'off') {
    $cmd = 'off';
} else {
    syntax();
}

my @intercept_progs = load();

if ($cmd eq 'status') {
    show_status();
} elsif ($cmd eq 'on') {
    on();
} elsif ($cmd eq 'off') {
    off();
} else {
    die "20cb5818-4b26-4aee-9d81-7c43854d0627";
}

sub syntax {
    print $SYNTAX;
    exit 240;
}

sub load {
    my $f = IO::File->new($intercept_progs_filename, 'r') || die "$0: can't read $intercept_progs_filename: $!\n";
    local $_;
    my @r = ();
    while ($_ = <$f>) {
        chomp;
        m,^(/.+?)=(/.+)$, or die "bad line $_ from $intercept_progs_filename";
        push(@r, [$1, $2]);
    }
    return @r;
}

# Print the current interception state.
sub show_status {
    print "Interception is:\n";
    for my $r (@intercept_progs) {
        my ($prog, $interceptor) = @$r;
        my $exists = -e $prog;
        my $exists_symlink = -l $prog;
        my $symlink_target;
        if ($exists_symlink) {
            $symlink_target = readlink($prog);
        }
        my $orig_exists = -e "${prog}_orig";

        if ($exists && !$exists_symlink && !$orig_exists) {
            print "OFF    for $prog\n";
        } elsif ($exists && $exists_symlink && $symlink_target eq $interceptor && $orig_exists) {
            print "ON     for $prog => $interceptor\n";
        } else {
            print "BROKEN for $prog\n";
        }
    }
}

sub check_root {
    if ($> != 0) {
        die "$0: need root access.\n";
    }
}

sub on {
    check_root();

    system("chmod", "a-w", $intercept_progs_filename)
        and warn "$0: couldn't chmod a-w $intercept_progs_filename\n";

    for my $r (@intercept_progs) {
        my ($prog, $interceptor) = @$r;
        # At this point, we shouldn't have any redundant links, since they've
        # been filtered out by list-programs-to-intercept.

        # TODO: be more robust if user has done something crazy, e.g. if
        # $prog doesn't exist.

        print "$prog:\n";
        if (! -e "${prog}_orig") {
            print "    rename $prog ${prog}_orig\n";
            rename($prog, "${prog}_orig") || die "$0: couldn't rename $prog to ${prog}_orig\n";

            die "$0: missing $interceptor\n" unless -f $interceptor && -x $interceptor;

            print "    symlink $interceptor $prog\n";
            symlink($interceptor, $prog)
                or die "$0: couldn't symlink $interceptor to $prog\n";
        } else {
            print "    [Nothing to do]\n";
        }
    }
}

sub off {
    check_root();

    for my $r (@intercept_progs) {
        my ($prog, $interceptor) = @$r;
        print "$prog:\n";
        if (-e "${prog}_orig") {
            if (-e $prog) {
                if (!-l $prog || readlink($prog) ne $interceptor) {
                    die "$0: both $prog and ${prog}_orig exist, but $prog is not a symlink to $interceptor!\n";
                }
                print "    unlink $prog\n";
                unlink($prog) or die "$0: couldn't unlink $prog\n";
            }
            print "    rename ${prog}_orig $prog\n";
            rename("${prog}_orig", $prog)
                or die "$0: couldn't rename ${prog}_orig $prog\n";
        } else {
            print "    [Nothing to do]\n";
        }
    }

    system("chmod", "u+w", $intercept_progs_filename)
        and warn "$0: couldn't chmod u+w $intercept_progs_filename\n";
}

sub is_build_interceptor_symlink {
    my ($f) = @_;
    return 0 unless -l $f;
    my $link = readlink($f);
    return 0 unless $link;
    return 0 unless $link =~ /build-interceptor/;
    return 1;
}
